buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath('com.github.jengelman.gradle.plugins:gradle-processes:0.3.0')
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'au.com.dius:pact-jvm-provider-gradle_2.11:2.1.13'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'spring-boot'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("au.com.dius:pact-jvm-consumer-junit_2.11:2.1.13")
    }
}

project(':microservices-pact-consumer') {
    jar {
        version = '0.0.1'
        baseName = 'microservices-pact-consumer'
    }

}


project(':microservices-pact-provider') {
    apply plugin: 'au.com.dius.pact'
    apply plugin: 'com.github.johnrengelman.processes'

    ext {
        pactFilePath = "$System.env.PACT_FILE"
    }

    task startProvider(type: com.github.jengelman.gradle.plugins.processes.tasks.JavaFork) {
        classpath = files("microservices-pact-provider/build/libs/microservices-pact-provider-${version}.jar")
        classpath += sourceSets.main.runtimeClasspath
        main = 'com.microservice.trainning.Application'
        doLast {
            Thread.sleep(10000)
        }
    }

    task stopProvider << {
        startProvider.processHandle.abort()
    }

    jar {
        version = '0.0.2'
        baseName = 'microservices-pact-provider'
    }

    pact {
        serviceProviders {
            fooProvider {
                startProviderTask = 'startProvider'
                terminateProviderTask = 'stopProvider'
                hasPactWith('fooConsumer') {
                    pactFile = file(pactFilePath)
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
